require import AllCore Distr DInterval List Int IntDiv.

from Jasmin require import JModel JBigNum.

require import W64_SchnorrProtocol.
require import BigNum_spec.
import W64xN. 


require W64_Zp_SchnorrCorrespondance.
clone import W64_Zp_SchnorrCorrespondance as Ind.

require Zp_SchnorrExtractability.
clone Zp_SchnorrExtractability as ZSC with 
   theory Zp_SchnorrCorrespondanceProofs.Zp_SchnorrInstance <- ZPSP.


section Extractability.

import ZPSP.
import ZpC DZmodP.
import W64xN Sub R. 


module  AWrapE(A:ZKRewindableMaliciousProverJ) : ZKRewindableMaliciousProverG = {
  proc commitment() : commitment  = {
     var c;
     c <@ A.commitment();
     return (inzmod (valR c ));
  }
  proc response(c:challenge) : response = {
   var r;
   r <@ A.response(bn_ofint c);
   return (valR r);
  }  

  proc getState = A.getState
  proc setState = A.setState
}.



module (ExtractorJ : ExtractorJ)(P : ZKRewindableMaliciousProverJ) = {
  module SA = ZSC.SpecialSoundnessAdversaryG(AWrapE(P))
  proc extract(p : W64xN.R.t) : W64xN.R.t = {
    var t1,t2;
    (t1,t2) <@ SA.attack(inzmod (valR p));
    return bn_ofint (ZSC.special_soundness_extractG t1 t2);
 }
}.


lemma q_less_modulusR x : 0 <= LSP.EG.asint x < modulusR.
split.
smt(@LSP.EG). move => pos.
smt(@LSP.EG q_less_p p_less_modulusR).
qed.

    (* LSP.soundness_relation  (ZPS.Sub.insubd (inzmod (valR s))) (LSP.EG.inzmod (valR res)) *)
lemma extractability_same : forall (P <:  ZKRewindableMaliciousProverJ) s &m,
 Pr[ ExtractorJ(P).extract(s) @&m: completeness_relationJ s res ] 
  = Pr[ ZSC.ExtractorG(AWrapE(P)).extract((inzmod (valR s))) @&m: completeness_relationG  (inzmod (valR s)) res ].
progress. 
byequiv.
proc.
call (_: ={glob P}). sim. auto. progress.
have : (valR (bn_ofint (ZSC.special_soundness_extractG result_R.`1 result_R.`2))) 
 = (ZSC.special_soundness_extractG result_R.`1 result_R.`2).
rewrite bn_ofintK. rewrite /special_soundness_extractG.  
rewrite /special_soundness_extract.  simplify. 
smt(@IntDiv q_less_modulusR). 
move => q.
have <- : (valR (bn_ofint (ZSC.special_soundness_extractG result_R.`1 result_R.`2))) 
 = (ZSC.special_soundness_extractG result_R.`1 result_R.`2).
rewrite bn_ofintK. rewrite /special_soundness_extractG. 
rewrite /special_soundness_extract. simplify.
smt(@IntDiv q_less_modulusR).
have -> : (valR (bn_ofint (ZSC.special_soundness_extractG result_R.`1 result_R.`2))) 
 = (ZSC.special_soundness_extractG result_R.`1 result_R.`2).
rewrite bn_ofintK. rewrite /special_soundness_extractG.  
simplify. smt(@IntDiv q_less_modulusR).
rewrite - q.
rewrite -  completness_compat.   
rewrite /completeness_relationJ.
rewrite H. auto.
rewrite completness_compat.
rewrite bn_ofintK.
have ->: (ZSC.special_soundness_extractG result_R.`1 result_R.`2 %% modulusR)
 = (ZSC.special_soundness_extractG result_R.`1 result_R.`2).
rewrite /special_soundness_extractG. smt(@IntDiv q_less_modulusR).
auto. auto. auto.
qed.



section.


module  AWrap(A:ZKMaliciousProverJ) : ZKMaliciousProverG = {
  proc commitment() : commitment  = {
     var c;
     c <@ A.commitment();
     return (inzmod (valR c ));
  }
  proc response(c:challenge) : response = {
   var r;
   r <@ A.response(bn_ofint c);
   return (valR r);
  }  
}.


lemma soundness_same (s : W64xN.R.t) &m : forall (A <: ZKMaliciousProverJ),
   Pr[ SoundnessJ(A,JVerifier).main(s)@&m : res]
   = Pr[ SoundnessG(AWrap(A),SchnorrVerifier).run(inzmod (W64xN.valR s))@&m : res].
move => A. byequiv. proc.
symmetry. call verify_eq.
inline AWrap(A).response.
wp. call (_:true).
wp. 
call challenge_eq.
inline AWrap(A).commitment.
wp. call (_:true). simplify. skip.
progress. smt. smt. smt. smt. auto. auto.
qed.


declare module P <: ZKRewindableMaliciousProverJ{-LSP.HV}.
declare axiom P_response_ll : islossless P.response.
declare axiom P_commitment_ll : islossless P.commitment.

declare axiom P_rewindable : exists (f : (glob AWrapE(P)) -> sbits),
  injective f /\
  (forall &m0,
     Pr[AWrapE(P).getState() @ &m0 :
        (glob AWrapE(P)) = (glob AWrapE(P)){m0} /\
        res = f (glob AWrapE(P)){m0}] =
     1%r) /\
  (forall &m0 (b : sbits) (x : (glob AWrapE(P))),
     b = f x => Pr[AWrapE(P).setState(b) @ &m0 : (glob AWrapE(P)) = x] = 1%r) /\
  islossless AWrapE(P).setState.


lemma extractabilityJ &m s: 
  Pr[ExtractorJ(P).extract(s)@&m: completeness_relationJ  s res ] >=
   (Pr[SoundnessJ(P, JVerifier).main(s) @ &m : res] ^ 2
       - 1%r / 2%r
           * Pr[SoundnessJ(P, JVerifier).main(s) @ &m : res]).
proof. rewrite (extractability_same P).
rewrite (soundness_same s &m P).
have -> : Pr[SoundnessG(AWrap(P), SchnorrVerifier).run(inzmod (valR s)) @ &m : res]
 = Pr[SoundnessG(AWrapE(P), SchnorrVerifier).run(inzmod (valR s)) @ &m : res].
byequiv. proc.  sim. auto. auto.
apply (ZSC.extractabilityG (AWrapE(P)) _ _ _ &m (inzmod (valR s))).
proc. call P_response_ll. auto.
proc. call P_commitment_ll. auto.
apply P_rewindable.
qed.

end section.

end section Extractability.
